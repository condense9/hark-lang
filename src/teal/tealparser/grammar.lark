// -*- mode : ebnf -*-
//
// https    ://github.com/antlr/grammars-v4/blob/master/clojure/Clojure.g4
// https    ://www.braveclojure.com/core-functions-in-depth/

start : file

file : ( "(" top_form ")" )*

?top_form : python
          | def

python : "importpy" IDENTIFIER ":from" IDENTIFIER [":as" IDENTIFIER]

def        : ("def"|"defun"|"define") IDENTIFIER "(" def_params ")" def_body  -> def_
def_params : IDENTIFIER*
?def_body  : do_block


?sexp : atom
      | "(" special_form ")"
      | list
      | reader_macro

?special_form : let | if | do | quote | async

list   : "(" sexp* ")"        -> list_
// map : "{" (sexp sexp)* "}" -> map_

async : "async" sexp+ -> async_

if : "if" sexp~3 -> if_

// _cond :  TODO

let      : "let" "(" bindings ")" do_block
bindings : binding*
binding  : "(" IDENTIFIER sexp ")"

?do      : "do" do_block
do_block : sexp*

quote : "quote" sexp


?reader_macro : m_quote | m_async | m_wait

m_quote  : "'" sexp
// m_map : "#" sexp
m_async  : "@" sexp
m_wait   : "$" sexp

// sharp : "#'" sexp


?atom   : symbol | literal
literal : string
        | int
        | float
        | true
        | false
        | nil

symbol : IDENTIFIER
true   : TRUE           -> true_
false  : FALSE          -> false_
string : ESCAPED_STRING -> string_
float  : SIGNED_FLOAT   -> float_
int    : SIGNED_INT     -> int_
nil    : NIL

// terminators, keywords
// See github.com/lark-parser/lark/blob/master/lark/grammars/common.lark

_STRING_INNER     : /.*?/
_STRING_ESC_INNER : _STRING_INNER /(?<!\\)(\\\\)*?/
ESCAPED_STRING    : "\"" _STRING_ESC_INNER "\""


DIGIT            : "0".."9"
HEXDIGIT         : "a".."f"|"A".."F"|DIGIT
INT              : DIGIT+
SIGNED_INT       : ["+"|"-"] INT
DECIMAL          : INT "." INT? | "." INT
_EXP             : ("e"|"E") SIGNED_INT
FLOAT            : INT _EXP | DECIMAL _EXP?
SIGNED_FLOAT     : ["+"|"-"] FLOAT
// NUMBER        : FLOAT | INT
// SIGNED_NUMBER : ["+"|"-"] NUMBER

TRUE  : "true"
FALSE : "false"

NIL : "nil"

IDENTIFIER : /[^\t\n\f,;\\ (){}'"`$]+/


COMMENT : /;[^\n]*/

WS : /[ \t\f\r\n]/+

%ignore COMMENT
%ignore WS
